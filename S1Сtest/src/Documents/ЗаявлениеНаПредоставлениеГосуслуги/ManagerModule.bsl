
Функция ЗначениеПоляЗаявления(Заявление, ВидПоля) Экспорт
	
	Если ТипЗнч(Заявление) = Тип("ДокументСсылка.ЗаявлениеНаПредоставлениеГосуслуги") Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Заявление", Заявление);
		Запрос.УстановитьПараметр("ВидПоля", ВидПоля);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаявлениеНаПредоставлениеГосуслугиПоляЗаявления.ЗначениеПоля КАК ЗначениеПоля
		|ИЗ
		|	Документ.ЗаявлениеНаПредоставлениеГосуслуги.ПоляЗаявления КАК ЗаявлениеНаПредоставлениеГосуслугиПоляЗаявления
		|ГДЕ
		|	ЗаявлениеНаПредоставлениеГосуслугиПоляЗаявления.Ссылка = &Заявление
		|	И ЗаявлениеНаПредоставлениеГосуслугиПоляЗаявления.Поле = &ВидПоля";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат Выборка.ЗначениеПоля;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Заявление) = Тип("ДокументОбъект.ЗаявлениеНаПредоставлениеГосуслуги") Тогда
		
		НайденныеСтроки = Заявление.ПоляЗаявления.НайтиСтроки(Новый Структура("Поле", ВидПоля));
		Если НайденныеСтроки.Количество() > 0 Тогда
			Возврат НайденныеСтроки[0].ЗначениеПоля;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции
