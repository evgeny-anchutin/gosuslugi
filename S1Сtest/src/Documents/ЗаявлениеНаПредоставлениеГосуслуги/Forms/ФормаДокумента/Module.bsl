
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьОграничениеТипаПолучателя();
	УстановитьПоляЗаявления();
	
КонецПроцедуры

&НаКлиенте
Процедура ГосУслугаПриИзменении(Элемент)
	
	УстановитьОграничениеТипаПолучателя();
	УстановитьПоляЗаявления();
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияПолучателяПриИзменении(Элемент)
	УстановитьПоляЗаявления();
КонецПроцедуры

&НаСервере
Процедура УстановитьОграничениеТипаПолучателя()
	
	МассивТипов = Новый Массив;
	СписокКатегорийПолучателя = Элементы.КатегорияПолучателя.СписокВыбора;
	СписокКатегорийПолучателя.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГосУслуга", Объект.ГосУслуга);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГосУслугиКатегорииПолучателей.Категория КАК Категория
	|ИЗ
	|	Справочник.ГосУслуги.КатегорииПолучателей КАК ГосУслугиКатегорииПолучателей
	|ГДЕ
	|	ГосУслугиКатегорииПолучателей.Ссылка = &ГосУслуга";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СписокКатегорийПолучателя.Добавить(Выборка.Категория);
		
		Если Выборка.Категория = Перечисления.КатегорияПолучателя.ФизическоеЛицо Тогда
			
			МассивТипов.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
			
		ИначеЕсли Выборка.Категория = Перечисления.КатегорияПолучателя.ЮридическоеЛицо Тогда
			
			МассивТипов.Добавить(Тип("СправочникСсылка.ЮридическиеЛица"));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.Получатель.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПоляЗаявления()
	
	ТаблицаПоляЗаявления = Объект.ПоляЗаявления.Выгрузить().СкопироватьКолонки();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ГосУслуга", Объект.ГосУслуга);
	Запрос.УстановитьПараметр("Категория", Объект.КатегорияПолучателя);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГосУслугиПоляЗаявления.Поле КАК Поле
	|ИЗ
	|	Справочник.ГосУслуги.ПоляЗаявления КАК ГосУслугиПоляЗаявления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ВидПоляЗаявления КАК ВидПоляЗаявления
	|		ПО ГосУслугиПоляЗаявления.Поле = ВидПоляЗаявления.Ссылка
	|ГДЕ
	|	ГосУслугиПоляЗаявления.Ссылка = &ГосУслуга
	|	И (ВидПоляЗаявления.КатегорияПолучателя = &Категория
	|			ИЛИ ВидПоляЗаявления.КатегорияПолучателя = ЗНАЧЕНИЕ(Перечисление.КатегорияПолучателя.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГосУслугиПоляЗаявления.НомерСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НайденныеСтроки = Объект.ПоляЗаявления.НайтиСтроки(Новый Структура("Поле", Выборка.Поле));
		
		СтрокаЗаявления = ТаблицаПоляЗаявления.Добавить();
		Если НайденныеСтроки.Количество() = 0 Тогда
			СтрокаЗаявления.Поле = Выборка.Поле;
		Иначе
			ЗаполнитьЗначенияСвойств(СтрокаЗаявления, НайденныеСтроки[0]);
		КонецЕсли;
		
	КонецЦикла;
	
	Объект.ПоляЗаявления.Загрузить(ТаблицаПоляЗаявления);
	
КонецПроцедуры
