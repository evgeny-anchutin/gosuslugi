
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокРолей();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРолей()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(Объект.ИдентификаторПользователяИБ);
	СписокРолей.Очистить();
	
	Для Каждого ТекущаяРоль Из Метаданные.Роли Цикл
		
		НовыйЭлементСпискаРолей = СписокРолей.Добавить(ТекущаяРоль.Имя, ТекущаяРоль.Синоним);
		
		Если ПользовательИБ <> Неопределено Тогда
			Если ПользовательИБ.Роли.Содержит(ТекущаяРоль) Тогда
				НовыйЭлементСпискаРолей.Пометка = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(Объект.ИдентификаторПользователяИБ);
	
	// Если пользователя нет, создаем
	
	Если ПользовательИБ = Неопределено Тогда
		
		ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
		ПользовательИБ.Имя = ТекущийОбъект.Наименование;
		ПользовательИБ.ПолноеИмя = ТекущийОбъект.ПолноеИмя;
		ПользовательИБ.Пароль = ТекущийОбъект.Пароль;
		
		УстановитьРолиПользователя(ПользовательИБ);
		
		ПользовательИБ.Записать();
		
		ТекущийОбъект.ИдентификаторПользователяИБ = ПользовательИБ.УникальныйИдентификатор;
		
	Иначе
		
		// Актуализируем информацию
		
		ИнформацияИзменена = Ложь;
		
		Если ПользовательИБ.Имя <> ТекущийОбъект.Наименование Тогда
			ПользовательИБ.Имя = ТекущийОбъект.Наименование;
			ИнформацияИзменена = Истина;
		КонецЕсли;
		
		Если ПользовательИБ.ПолноеИмя <> ТекущийОбъект.ПолноеИмя Тогда
			ПользовательИБ.ПолноеИмя = ТекущийОбъект.ПолноеИмя;
			ИнформацияИзменена = Истина;
		КонецЕсли;
		
		ПользовательИБ.Пароль = ТекущийОбъект.Пароль;

		ИнформацияИзменена = УстановитьРолиПользователя(ПользовательИБ) Или ИнформацияИзменена;

		ПользовательИБ.Записать();
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры

&НаСервере
Функция УстановитьРолиПользователя(ПользовательИБ)
	
	РолиИзменены = Ложь;
	
	Для Каждого ЭлементСпискаРолей Из СписокРолей Цикл
		
		ТекущаяРоль = Метаданные.Роли.Найти(ЭлементСпискаРолей.Значение);
		
		Если ЭлементСпискаРолей.Пометка Тогда
		
			Если Не ПользовательИБ.Роли.Содержит(ТекущаяРоль) Тогда
				ПользовательИБ.Роли.Добавить(ТекущаяРоль);
				РолиИзменены = Истина;
			КонецЕсли;
			
		Иначе
		
			Если ПользовательИБ.Роли.Содержит(ТекущаяРоль) Тогда
				ПользовательИБ.Роли.Удалить(ТекущаяРоль);
				РолиИзменены = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РолиИзменены;
	
КонецФункции

&НаКлиенте
Процедура ПолноеИмяПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		Объект.Наименование = Объект.ПолноеИмя;
	КонецЕсли;
	
КонецПроцедуры
